buildscript {
    repositories {
        mavenCentral()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'


repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven {
        url "https://maven.google.com"
    }
    jcenter()
}

dependencies {

    implementation fileTree(include: '*.jar', dir: 'libs')

    implementation 'com.google.android.material:material:1.1+'

    implementation 'com.google.android.gms:play-services-base:17.1.0'

    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
    implementation "com.google.firebase:firebase-messaging:20.1.0"

    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation 'com.github.vajro:MaterialDesignLibrary:1.6'

    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1'
    implementation 'androidx.preference:preference:1.1.0'

    implementation 'com.nhaarman.listviewanimations:library:2.6.0'

    implementation 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'

    implementation 'com.google.firebase:firebase-core:17.2.2'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    //  implementation 'com.google.firebase:firebase-crash:11.8.0'

    implementation 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    implementation 'com.github.castorflex.smoothprogressbar:library-circular:1.3.0'

    // twitter
    implementation ('com.twitter.sdk.android:twitter:3.3.0@aar') {
        transitive = true
    }

    // yt embed
    // implementation 'com.github.PierfrancescoSoffritti:AndroidYouTubePlayer:4.1.4'
    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:8.0.1'

    // mp4
    implementation 'com.google.android.exoplayer:exoplayer:2.11.1'

    implementation 'com.vdurmont:emoji-java:5.1.1'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}

android {
    useLibrary 'org.apache.http.legacy'
    signingConfigs {
        release
    }
    compileSdkVersion 29
    buildToolsVersion '29.0.0'
    compileOptions.encoding = 'windows-1251'
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        androidTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    defaultConfig {
        applicationId "net.woggle.shackbrowse"
        minSdkVersion 17
        targetSdkVersion 29
        multiDexEnabled false
    }
    productFlavors {
    }
    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions{
        javaMaxHeapSize "4g"
    }
    configurations {
        all {
            exclude module: 'httpclient'
            exclude module: 'json'
            exclude group: 'org.apache.httpcomponents'
        }
    }
}

def propFile = file('../signing.properties')
if (propFile.canRead()) {
    def Properties p = new Properties()
    p.load(new FileInputStream(propFile))

    if (p != null
            && p.containsKey('STORE_FILE')
            && p.containsKey('STORE_PASSWORD')
            && p.containsKey('KEY_ALIAS')
            && p.containsKey('KEY_PASSWORD')
    ) {
        println "RELEASE_BUILD: Signing..."

        android.signingConfigs.release.storeFile = file(p['STORE_FILE'])
        android.signingConfigs.release.storePassword = p['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = p['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = p['KEY_PASSWORD']

    } else {
        println "RELEASE_BUILD: Required properties in signing.properties are missing"
        android.buildTypes.release.signingConfig = null
    }

} else {
    println "RELEASE_BUILD: signing.properties not found"
    android.buildTypes.release.signingConfig = null
}
apply plugin: 'com.google.gms.google-services';